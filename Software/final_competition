#include <Adafruit_MotorShield.h>
#include <NewPing.h>

// Pin numbers
#define pinLL 2
#define pinL 7
#define pinR 6
#define pinRR 5
#define pinFrontTrig 13
#define pinFrontEcho 12
#define pinSideTrig 8
#define pinSideEcho 11
#define pinMoving A0
#define pinButton A1
#define pinBlueDetector 1
#define pinRedDetector 0
#define pinBlue A2
#define pinRed A3

// Global constants
#define ROT_TIME 160
#define MOVE_TIME 70
#define TUNNEL_DISTANCE 8
#define INTERVAL 1000
#define BLUE_TIME 12000
#define RED_TIME 27000
#define TURN_DELAY 1050
#define MOVE_DELAY 2000
#define TOPSPEED 255
const int SLOWDOWN = TOPSPEED * 0.6;

bool colorRed=true;
bool tunnel_passed = false;

// Initialize ultrasound
NewPing sonarFront(pinFrontTrig, pinFrontEcho, 6); //change range
NewPing sonarSide(pinSideTrig, pinSideEcho, 15);

// Initialize motors and sensors
Adafruit_MotorShield AFMS = Adafruit_MotorShield();
Adafruit_DCMotor *motorLeft = AFMS.getMotor(3);
Adafruit_DCMotor *motorRight = AFMS.getMotor(4);

// Start state for leaving and returning to starting box
int state = 0;

// Flags and booleans
bool set = false;
bool setStop = false;
bool endRed = false;

// Button state
bool button = false;

// Initialize timers
unsigned long start = 0;
unsigned long tunnel_start = millis() + 999999999;

// Motor speeds
int speedLeft = 0;
int speedRight = 0;

// Line sensor variables
int ll = 0;
int l = 0;
int r = 0;
int rr= 0;

// Turn left 90 degrees
void turnLeft() {
  delay(TURN_DELAY);
  motorLeft -> setSpeed(TOPSPEED);
  motorRight-> setSpeed(TOPSPEED);
  motorLeft -> run(BACKWARD);
  motorRight-> run(FORWARD);
  delay(MOVE_DELAY);
  motorLeft -> run(FORWARD);
  speedLeft = TOPSPEED;
  speedRight = TOPSPEED;
  motorLeft -> setSpeed(speedLeft);
  motorRight -> setSpeed(speedRight);
}

// Turn right 90 degrees
void turnRight() {
  delay(TURN_DELAY);
  motorLeft -> setSpeed(TOPSPEED);
  motorRight-> setSpeed(TOPSPEED);
  motorLeft -> run(FORWARD);
  motorRight-> run(BACKWARD);
  delay(MOVE_DELAY);
  motorRight -> run(FORWARD);
  speedLeft = TOPSPEED;
  speedRight = TOPSPEED;
  motorLeft -> setSpeed(speedLeft);
  motorRight -> setSpeed(speedRight);
}

// Right turn after placing block without any intial forward movement
void turnRightEnd() {
  motorLeft -> setSpeed(TOPSPEED);
  motorRight-> setSpeed(TOPSPEED);
  motorLeft -> run(FORWARD);
  motorRight-> run(BACKWARD);
  delay(MOVE_DELAY);
  motorRight -> run(FORWARD);
  speedLeft = TOPSPEED;
  speedRight = TOPSPEED;
  motorLeft -> setSpeed(speedLeft);
  motorRight -> setSpeed(speedRight);
}

// Left turn after placing block without any intial forward movement
void turnLeftEnd() {
  motorLeft -> setSpeed(TOPSPEED);
  motorRight-> setSpeed(TOPSPEED);
  motorLeft -> run(BACKWARD);
  motorRight-> run(FORWARD);
  delay(MOVE_DELAY * 1.2);
  motorRight -> run(FORWARD);
  speedLeft = TOPSPEED;
  speedRight = TOPSPEED;
  motorLeft -> setSpeed(speedLeft);
  motorRight -> setSpeed(speedRight);
}

// Decision making when junctions are reached
void junctionDetector() {
  switch (state) {
    case 0:
        if (ll == 1 || rr == 1) {
          state = 1;
          // Start junction reading "debounce" timer
          start = millis();
        } 
      break;
    case 1:
      if ((ll == 1 || rr == 1) && (millis() > start + INTERVAL)) {
        // Turn right when leaving or re-entering box from blue block area
        if (endRed == false) {
          turnRight();
          if (setStop == true) {
            motorLeft -> run(FORWARD);
            motorRight -> run(FORWARD);
            delay(3000);
            speedLeft = 0;
            speedRight = 0;
            motorLeft -> setSpeed(speedLeft);
            motorRight -> setSpeed(speedRight);
            delay(99999999);
          }
        }
        // Turn levt when re-entering box from red block area
        else {
          turnLeft();
          motorLeft -> run(FORWARD);
          motorRight -> run(FORWARD);
          delay(3000);
          speedLeft = 0;
          speedRight = 0;
          motorLeft -> setSpeed(speedLeft);
          motorRight -> setSpeed(speedRight);
          delay(99999999);
        }
        start = millis();
        state = -1;
      }
      break;
  }
}

// Tunnel navigation using ultrasound
void tunnelNavigation() {
  int diff = sonarSide.ping_cm() - TUNNEL_DISTANCE;
  // Too far right, move left
  if (diff > 0) {
    if (speedLeft != SLOWDOWN || speedRight != SLOWDOWN) {
      motorLeft -> setSpeed(SLOWDOWN);
      motorRight -> setSpeed(SLOWDOWN);
      speedLeft = SLOWDOWN;
      speedRight = SLOWDOWN;
    }
    motorLeft -> run(BACKWARD);
    motorRight -> run(FORWARD);
    delay(ROT_TIME * 0.4);
    motorLeft -> run(FORWARD);
    delay(MOVE_TIME * 2);
  }
  // Too far left, move right
  else if (diff < 0) {
    if (speedLeft != SLOWDOWN || speedRight != SLOWDOWN) {
      motorLeft -> setSpeed(SLOWDOWN);
      motorRight -> setSpeed(SLOWDOWN);
      speedLeft = SLOWDOWN;
      speedRight = SLOWDOWN;
    }
    motorLeft -> run(FORWARD);
    motorRight -> run(BACKWARD);
    delay(ROT_TIME * 0.4);
    motorRight -> run(FORWARD);
    delay(MOVE_TIME * 2);
  }
  // Correct position, travel straight
  else {
    if (speedLeft != TOPSPEED || speedRight != TOPSPEED) {
      motorLeft -> setSpeed(TOPSPEED);
      motorRight -> setSpeed(TOPSPEED);
      motorLeft -> run(FORWARD);
      motorRight -> run(FORWARD);
      speedLeft = TOPSPEED;
      speedRight = TOPSPEED;
    }
  }
}

// Line following
void lineFollowing() {
  // Take readings from junction detectors first
  ll = digitalRead(pinLL);
  rr = digitalRead(pinRR);
  // Ignore line sensor readings when on junction
  if (ll == 1 || rr == 1) {
    l = 0;
    r = 0;
  }
  else {
    l = digitalRead(pinL);
    r = digitalRead(pinR);
  }
  // Use junction detectors as line followers when traversing ramp
  if (state > 1 && !tunnel_passed) {
    if (ll == 1) {
      l = 1;
    }
    if (rr == 1) {
      r = 1;
    }
  }
  // Apply decision making after junction is detected
  junctionDetector();
  // Robot has moved too far to the right, moves left
  if (l == 1) {
    if (speedLeft != SLOWDOWN || speedRight != SLOWDOWN) {
      speedLeft = SLOWDOWN;
      speedRight = SLOWDOWN;
      motorLeft -> setSpeed(speedLeft);
      motorRight -> setSpeed(speedRight);
    }
    motorLeft -> run(BACKWARD);
    motorRight -> run(FORWARD);
    delay(ROT_TIME);
    motorLeft -> run(FORWARD);
    delay(MOVE_TIME);
  }
  // Robot has moved too far to the left, moves right
  if (r == 1) {
    if (speedLeft != SLOWDOWN || speedRight != SLOWDOWN) {
      speedLeft = SLOWDOWN;
      speedRight = SLOWDOWN;
      motorLeft -> setSpeed(speedLeft);
      motorRight -> setSpeed(speedRight);
    }
    motorLeft -> run(FORWARD);
    motorRight -> run(BACKWARD);
    delay(ROT_TIME);
    motorRight -> run(FORWARD);
    delay(MOVE_TIME);
  }
  // Robot is positioned correctly, continue forwards
  if (l == 0 && r == 0) {
    if (speedLeft != TOPSPEED || speedRight != TOPSPEED){
      speedLeft = TOPSPEED;
      speedRight = TOPSPEED;
      motorLeft -> setSpeed(speedLeft);
      motorRight -> setSpeed(speedRight);
      motorLeft -> run(FORWARD);
      motorRight -> run(FORWARD);
    }
  }
}

// Pin and motor setup
void setup() {
  Serial.begin(9600);
  AFMS.begin();
  pinMode(pinMoving, OUTPUT);
  pinMode(pinLL, INPUT);
  pinMode(pinL, INPUT);
  pinMode(pinR, INPUT);
  pinMode(pinRR, INPUT);
  pinMode(pinSideTrig, OUTPUT);
  pinMode(pinSideEcho, INPUT);
  pinMode(pinButton,INPUT);
  pinMode(pinBlueDetector, INPUT); 
  pinMode(pinRedDetector, INPUT); 
  pinMode(pinBlue, OUTPUT); 
  pinMode(pinRed, OUTPUT);
  motorLeft -> setSpeed(speedLeft) ;
  motorRight -> setSpeed(speedRight);
  motorLeft -> run(FORWARD);
  motorRight -> run(FORWARD);
  digitalWrite(pinMoving, HIGH);
  start = millis();
}

// Main loop
void loop() {
  // Initial button press detection
  if (digitalRead(pinButton) == HIGH && !button) {
    button = true;
    speedLeft = TOPSPEED;
    speedRight = TOPSPEED;
    motorLeft -> setSpeed(speedLeft) ;
    motorRight -> setSpeed(speedRight);
    motorLeft -> run(FORWARD);
    motorRight -> run(FORWARD);
    digitalWrite(pinMoving, LOW);
    delay(300);
  }
  // Executed once button has been pressed once
  if (button) {
    // Executes tunnel navigation when critical distance from sensor detected
    if (sonarSide.ping_cm() != 0) {
      tunnelNavigation();
      tunnel_passed = true;
    }
    // Executes after tunnel has passed
    else if (tunnel_passed) {
      if (set == false) {
        // Start timer since leaving tunnel
        tunnel_start = millis();
        set = true;
      }
      lineFollowing();
      // After 12 seconds of line following, turn into blue box
      if (tunnel_passed && !colorRed && millis() > tunnel_start + BLUE_TIME) {
        turnRight();
        motorLeft -> setSpeed(TOPSPEED);
        motorRight -> setSpeed(TOPSPEED);
        motorLeft -> run(FORWARD);
        motorRight -> run(FORWARD);
        delay(1500);
        motorLeft -> run(BACKWARD);
        motorRight -> run(BACKWARD);
        motorLeft -> setSpeed(TOPSPEED);
        motorRight -> setSpeed(TOPSPEED);
        speedLeft = TOPSPEED;
        speedRight = TOPSPEED;
        delay(1500);
        motorLeft -> run(FORWARD);
        motorRight -> run(FORWARD);
        turnLeftEnd();
        state = 1;
        endRed = false;
        tunnel_passed=false;
        start = millis();
      }
      // After 27 seconds of line following, turn into red box
      else if (tunnel_passed && millis() > tunnel_start + RED_TIME) {
        turnRight();
        motorLeft -> setSpeed(TOPSPEED);
        motorRight -> setSpeed(TOPSPEED);
        motorLeft -> run(FORWARD);
        motorRight -> run(FORWARD);
        delay(1500);
        motorLeft -> run(BACKWARD);
        motorRight -> run(BACKWARD);
        motorLeft -> setSpeed(TOPSPEED);
        motorRight -> setSpeed(TOPSPEED);
        speedLeft = TOPSPEED;
        speedRight = TOPSPEED;
        delay(1500);
        motorLeft -> run(FORWARD);
        motorRight -> run(FORWARD);
        turnRightEnd();
        state = 1;
        endRed = true;
        tunnel_passed = false;
        start = millis();
      }
    }
    else {
      lineFollowing();
    }
    // Code for block detection
    if (sonarFront.ping_cm() != 0 && setStop == false) {
      setStop = true;
      delay(1500);
      motorLeft -> setSpeed(0);
      motorRight -> setSpeed(0);
      // Take 10 readings from colour sensor
      for(int i = 0; i < 10; i++){
        if(digitalRead(pinRedDetector) == HIGH) {
          colorRed = false;
          endRed = false;
        }
        delay(50);
      }
      // Turn on appropriate LED
      if (colorRed) {
        digitalWrite(pinRed, HIGH);
      }
      else {
        digitalWrite(pinBlue, HIGH);
      }
      delay(4500);
      digitalWrite(pinRed, LOW);
      digitalWrite(pinBlue, LOW);
      speedLeft = TOPSPEED;
      speedRight = TOPSPEED;
      motorLeft -> setSpeed(speedLeft);
      motorRight -> setSpeed(speedRight);
    }
    delay(50);
  }
  // Flash LED at 2Hz while moving
  if (speedLeft != 0 || speedRight != 0) {
    digitalWrite(pinMoving,LOW);  
  } 
  else {
    digitalWrite(pinMoving, HIGH);
  }
}
